buildscript {
	repositories {
		mavenCentral()

		maven {
			url = 'http://files.minecraftforge.net/maven'
		}

		maven {
			url = 'http://repo.spongepowered.org/maven'
		}

		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:4.+"
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}

plugins {
	id 'org.spongepowered.plugin' version '0.9.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = "${mod_version}"
group = "${mod_group}"
archivesBaseName = "${mod_name}-${version_minecraft}"
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Set up forge options
minecraft {
	mappings channel: 'snapshot', version: project.version_mcp
	// replace '@VERSION@', project.version
	runs {
		client {
			workingDirectory project.file('rundir')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
			mods {
			}
		}

		server {
			workingDirectory project.file('rundir')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
			mods {
			}
		}
	}
}

mixin {
	add sourceSets.main, "mixins.ic2energyfix.refmap.json"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.version_minecraft

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.version_minecraft
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

configurations {
	// embed
	shade
	implementation.extendsFrom shade
}

repositories {
	flatDir {
		dirs 'lib'
	}

	maven {
		name = "sponge"
		url = "https://repo.spongepowered.org/maven/"
	}

	maven {
		name = "forge"
		url = "https://files.minecraftforge.net/maven"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:$project.version_minecraft-$project.version_forge"
	compile name: 'industrialcraft-2-2.8.222-ex112-dev'

	annotationProcessor 'org.spongepowered:mixin:0.8.1-SNAPSHOT:processor'

	shade (group: "org.spongepowered", name: "mixin", version: "0.8.1-SNAPSHOT") {
		exclude group: 'org.ow2.asm', module: 'asm-tree'
	}

	// embed("org.spongepowered:mixin:0.8-SNAPSHOT") { transitive = false }
}

jar {
	from(configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }) {
		exclude "LICENSE.txt", "META-INF/MANIFEST.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
	}

	manifest.attributes(
			"FMLCorePluginContainsFMLMod": "false",
			"ForceLoadAsMod": "true",
			"MixinCompatibilityLevel": "JAVA_8",
			"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
			"TweakOrder": "100",
			"MixinConfigs": "mixins.ic2energyfix.json",
			"FMLCorePlugin": "com.eric0210.ic2energyfix.IC2EnergyFix"
	)
}

reobf {
	jar {
		classpath = sourceSets.main.compileClasspath
	}
}

build.dependsOn reobfJar
